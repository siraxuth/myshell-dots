pragma Singleton

import QtQuick
import Quickshell
import Quickshell.Io
import Caelestia.Config
import Caelestia.Models
import qs.services
import qs.utils

Searcher {
    id: root

    readonly property string currentNamePath: `${Paths.state}/wallpaper/path.txt`
    readonly property list<string> smartArg: GlobalConfig.services.smartScheme ? [] : ["--no-smart"]

    property bool showPreview: false
    readonly property string current: showPreview ? previewPath : actualCurrent
    property string previewPath
    property string actualCurrent
    property bool previewColourLock
    property bool pendingPreviewClear

    property var propertiesCache: ({})

    // Live wallpaper settings
    property bool disableAnimations: false
    property int animationDuration: 500
    readonly property bool animsEnabled: !root.disableAnimations

    property bool behaviorEnabled: true
    property bool batteryLimitEnabled: true
    property int batteryLimit: 40
    property bool pauseOnFullscreen: true
    property bool pauseOnGameMode: true
    property int maxFps: 30
    property bool settingsLoaded: false

    FileView {
        id: liveSettingsView
        path: `${Paths.config}/Wallpaper_Settings.json`
        watchChanges: true
        printErrors: false
        onLoaded: {
            try {
                const data = JSON.parse(text().trim());
                if (data.disableAnimations !== undefined) root.disableAnimations = data.disableAnimations;
                else root.disableAnimations = false;
                if (data.animationDuration !== undefined) {
                    const v = Number(data.animationDuration);
                    root.animationDuration = Number.isFinite(v) ? Math.max(1, Math.min(2000, v)) : 500;
                } else {
                    root.animationDuration = 500;
                }
                if (data.behaviorEnabled !== undefined) root.behaviorEnabled = data.behaviorEnabled;
                if (data.batteryLimitEnabled !== undefined) root.batteryLimitEnabled = data.batteryLimitEnabled;
                if (data.batteryLimit !== undefined) root.batteryLimit = data.batteryLimit;
                if (data.pauseOnFullscreen !== undefined) root.pauseOnFullscreen = data.pauseOnFullscreen;
                if (data.pauseOnGameMode !== undefined) root.pauseOnGameMode = data.pauseOnGameMode;
                if (data.maxFps !== undefined) {
                    const fps = Number(data.maxFps);
                    root.maxFps = Number.isFinite(fps) ? Math.max(0, Math.min(60, Math.round(fps))) : 30;
                } else {
                    root.maxFps = 30;
                }
            } catch(e) {
                root.disableAnimations = false;
                root.animationDuration = 500;
            }
            root.settingsLoaded = true;
        }
        onLoadFailed: err => {
            root.settingsLoaded = true;
            if (err === FileViewError.FileNotFound)
                Qt.callLater(() => root.saveSettings());
        }
    }

    function saveSettings(): void {
        let data = {
            disableAnimations: root.disableAnimations,
            animationDuration: root.animationDuration,
            behaviorEnabled: root.behaviorEnabled,
            batteryLimitEnabled: root.batteryLimitEnabled,
            batteryLimit: root.batteryLimit,
            pauseOnFullscreen: root.pauseOnFullscreen,
            pauseOnGameMode: root.pauseOnGameMode,
            maxFps: root.maxFps
        };
        liveSettingsView.setText(JSON.stringify(data, null, 4));
    }

    FileView {
        id: propsFileView
        path: `${Paths.home}/.cache/caelestia/wallpaper_properties.json`
        watchChanges: true
        printErrors: false
        onLoaded: {
            try {
                root.propertiesCache = JSON.parse(text().trim());
            } catch(e) {}
        }
    }

    function isVideoPath(path: string): bool {
        return typeof path === "string" && /\.(mp4|mkv|webm|avi|mov)$/i.test(path);
    }

    function cleanWallpaperPath(path: string): string {
        return String(path || "").replace(/^file:\/\//, "");
    }

    function playbackPath(path: string): string {
        const clean = cleanWallpaperPath(path);
        if (!clean) return clean;
        const entry = propertiesCache[clean] || propertiesCache[path];
        if (entry && entry.playback) return cleanWallpaperPath(entry.playback);
        return clean;
    }

    function hasPlaybackCache(path: string): bool {
        const clean = cleanWallpaperPath(path);
        if (!clean) return false;
        const entry = propertiesCache[clean] || propertiesCache[path];
        return !!(entry && entry.playback);
    }

    function ensurePlaybackCache(path: string): void {
        const clean = cleanWallpaperPath(path);
        if (!isVideoPath(clean) || hasPlaybackCache(clean) || transcodeProc.running)
            return;
        transcodeProc.filePath = clean;
        transcodeProc.running = true;
    }

    function videoThumbPath(path: string): string { return thumbPath(path); }

    function thumbPath(path: string): string {
        const clean = cleanWallpaperPath(path);
        if (!clean) return "";
        const parts = clean.split("/");
        if (parts.length < 3) return "";
        const homeDir = "/" + parts[1] + "/" + parts[2];
        const fileName = parts[parts.length - 1];
        return `${homeDir}/.cache/caelestia/live_thumbs/${fileName}.jpg`;
    }

    function setRandom(): void {
        let arr = [];
        if (wallpapers.entries) {
            for (let i = 0; i < wallpapers.entries.length; i++)
                arr.push(wallpapers.entries[i].path);
        }
        if (liveWallpapers.entries) {
            for (let i = 0; i < liveWallpapers.entries.length; i++)
                arr.push(liveWallpapers.entries[i].path);
        }
        if (arr.length > 0)
            setWallpaper(arr[Math.floor(Math.random() * arr.length)]);
    }

    function setWallpaper(path: string): void {
        actualCurrent = path;
        ensurePlaybackCache(path);
        Quickshell.execDetached(["caelestia", "wallpaper", "-f", path, ...smartArg]);
    }

    function preview(path: string): void {
        if (!path) { stopPreview(); return; }
        if (showPreview && previewPath === path) return;

        ensurePlaybackCache(path);
        previewPath = path;
        showPreview = true;

        if (Colours.scheme === "dynamic") {
            if (getPreviewColoursProc.running)
                getPreviewColoursProc.running = false;
            Qt.callLater(() => {
                if (showPreview && previewPath === path && Colours.scheme === "dynamic")
                    getPreviewColoursProc.running = true;
            });
        }
    }

    function stopPreview(): void {
        showPreview = false;
        if (previewColourLock)
            pendingPreviewClear = true;
        else
            Colours.showPreview = false;
    }

    onPreviewColourLockChanged: {
        if (!previewColourLock && pendingPreviewClear)
            Colours.showPreview = false;
    }

    function refreshWallpapers(): void {
        refreshProc.running = true;
    }

    Process {
        id: transcodeProc
        property string filePath: ""
        command: ["bash", "-c", `"/usr/bin/python3" "${Paths.home}/.local/bin/update-caelestia-live-thumbs" --file "${filePath}" "${Paths.wallsdir}" "${liveWallpapers.path}" </dev/null`]
        onRunningChanged: {
            if (!running && filePath)
                propsFileView.reload();
        }
    }

    Process {
        id: refreshProc
        command: ["bash", "-c", `"/usr/bin/python3" "${Paths.home}/.local/bin/update-caelestia-live-thumbs" "${Paths.wallsdir}" "${liveWallpapers.path}" </dev/null`]
        onRunningChanged: {
            if (!running) {
                let oldPath = liveWallpapers.path;
                let oldPath2 = wallpapers.path;
                let oldPropsPath = propsFileView.path;
                liveWallpapers.path = "";
                wallpapers.path = "";
                propsFileView.path = "";
                Qt.callLater(() => {
                    liveWallpapers.path = oldPath;
                    wallpapers.path = oldPath2;
                    propsFileView.path = oldPropsPath;
                });
            }
        }
    }

    property int filterMode: 0
    property string colorFilter: ""

    function cycleFilterMode(reverse = false): void {
        const order = [2, 0, 1];
        let idx = order.indexOf(filterMode);
        if (idx === -1) idx = 0;
        if (reverse) idx = (idx - 1 + order.length) % order.length;
        else idx = (idx + 1) % order.length;
        filterMode = order[idx];
    }

    function cycleColorFilter(reverse = false): void {
        const colors = ["", "red", "orange", "yellow", "green", "blue", "purple", "pink", "white", "black"];
        let idx = colors.indexOf(colorFilter);
        if (idx === -1) idx = 0;
        if (reverse) idx = (idx - 1 + colors.length) % colors.length;
        else idx = (idx + 1) % colors.length;
        colorFilter = colors[idx];
    }

    function matchesColor(path: string, filter: string): bool {
        if (!filter || filter === "" || filter === "all") return true;
        let cleanPath = String(path).replace(/^file:\/\//, "");
        let entry = propertiesCache[cleanPath] || propertiesCache[path];
        if (!entry || typeof entry === "string") return false;
        if (entry.color === filter) return true;
        if (entry.colors && Array.isArray(entry.colors)) {
            if (entry.colors.includes(filter)) return true;
        }
        return false;
    }

    property var allEntries: {
        let arr = [];
        if (filterMode === 0 || filterMode === 2) {
            if (wallpapers.entries) {
                for (let i = 0; i < wallpapers.entries.length; i++) {
                    let entry = wallpapers.entries[i];
                    if (matchesColor(entry.path, colorFilter))
                        arr.push(entry);
                }
            }
        }
        if (filterMode === 1 || filterMode === 2) {
            if (liveWallpapers.entries) {
                for (let i = 0; i < liveWallpapers.entries.length; i++) {
                    let entry = liveWallpapers.entries[i];
                    if (matchesColor(entry.path, colorFilter))
                        arr.push(entry);
                }
            }
        }
        return arr;
    }

    list: allEntries
    key: "relativePath"
    useFuzzy: GlobalConfig.launcher.useFuzzy.wallpapers
    extraOpts: useFuzzy ? ({}) : ({
            forward: false
        })

    IpcHandler {
        function get(): string {
            return root.actualCurrent;
        }

        function set(path: string): void {
            root.setWallpaper(path);
        }

        function list(): string {
            return root.list.map(w => w.path).join("\n");
        }

        target: "wallpaper"
    }

    FileView {
        path: root.currentNamePath
        watchChanges: true
        printErrors: false
        onFileChanged: reload()
        onLoaded: {
            root.actualCurrent = text().trim();
            root.previewColourLock = false;
        }
    }

    FileSystemModel {
        id: wallpapers

        recursive: true
        path: Paths.wallsdir
        filter: FileSystemModel.Images
    }

    FileSystemModel {
        id: liveWallpapers

        recursive: true
        path: Quickshell.env("CAELESTIA_LIVE_WALLPAPERS_DIR") || `${Paths.videos}/Wallpapers`
        filter: FileSystemModel.Files
    }

    Process {
        id: getPreviewColoursProc

        command: ["caelestia", "wallpaper", "-p", root.previewPath, ...root.smartArg]
        stdout: StdioCollector {
            onStreamFinished: {
                Colours.load(text, true);
                Colours.showPreview = true;
            }
        }
    }
}
